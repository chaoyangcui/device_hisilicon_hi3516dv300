# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/config/clang/clang.gni")

kernel_build_script_dir = "//kernel/linux/patches"
kernel_source_dir = "//kernel/linux-4.19"

kernel_module_file_list = [
  "hi3516cv500_base.ko",
  "hi3516cv500_hdmi.ko",
  "hi3516cv500_ir.ko",
  "hi3516cv500_sys.ko",
  "hi3516cv500_tde.ko",
  "hi3516cv500_vo_dev.ko",
  "hi3516cv500_wdt.ko",
  "hi_cipher.ko",
  "hifb.ko",
  "hi_irq.ko",
  "hi_mipi_rx.ko",
  "hi_mipi_tx.ko",
  "hi_osal.ko",
  "hi_proc.ko",
  "sys_config.ko",
]

action("kernel") {
  script = "build_kernel.sh"
  sources = [ kernel_source_dir ]

  product_path="vendor/$product_company/$product_name"
  outputs = [ "$root_build_dir/packages/phone/images/uImage" ]
  args = [
    rebase_path(kernel_build_script_dir, root_build_dir),
    rebase_path("$root_out_dir/../KERNEL_OBJ"),
    rebase_path("$root_build_dir/packages/phone/images"),
    device_type,
    rebase_path("$clang_base_path"),
    product_path
  ]
}

action("modules") {
  script = "copy_modules.sh"
  deps = [ ":kernel" ]
  kernel_module_file_list_out = []
  foreach(file, kernel_module_file_list) {
    kernel_module_file_list_out += [ "$root_out_dir/kernel/modules/$file" ]
  }
  outputs = kernel_module_file_list_out
  args = [
    rebase_path("$root_out_dir/../KERNEL_OBJ"),
    rebase_path("$root_out_dir/kernel/modules"),
  ]
}

## declare modules to install kernel modules to vendor

kernel_modules = get_target_outputs(":modules")
kernel_modules_targets = []

foreach(km, kernel_modules) {
  _name = get_path_info(km, "file")
  ohos_prebuilt_shared_library(_name) {
    deps = [ ":modules" ]
    source = km
    module_install_dir = "modules"
    install_images = [ "vendor" , "updater"]
    part_name = "hisilicon_products"
  }
  kernel_modules_targets += [ ":$_name" ]
}

group("all_modules") {
  deps = kernel_modules_targets
}
